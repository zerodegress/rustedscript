block = puncBraceLeft, statements, puncBraceRight
  | puncBraceLeft, puncBraceRight;

statements = statment, statements
  | statement;

statement = expr, [puncSemi];

annotation = puncAt, identifier, puncParrenLeft, [tupleExpr], puncParrenRight
  | puncAt, identifier;

annotations = annotation, annotations
  | annotation;

functionDeclaration = keywordFn, identifier, puncParrenLeft, [tupleExpr], puncParrenRight, block;

expr = statementExpr
  | computeExpr;

statementExpr = functionDeclarationExpr
  | blockExpr
  | bindDeclarationExpr;

computeExpr = tupleExpr;

functionDeclarationExpr = functionDeclaration;

blockExpr = block;

bindDeclarationExpr = keywordLet, [keywordMut], tupleExpr;

tupleExpr = annotationExpr, puncComma, tupleExpr
  | annotationExpr, puncComma
  | annotationExpr;

annotationExpr = annotation, annotationExpr
  | assignExpr;

assignExpr = addExpr, puncAssign, assignExpr
  | orExpr;

orExpr = andExpr, opOr, orExpr
  | andExpr;

andExpr = gtExpr, opAnd, andExpr
  | gtExpr;

gtExpr = eqExpr, opGt, gtExpr
  | eqExpr, opGe, gtExpr
  | eqExpr, opLt, gtExpr
  | eqExpr, opLe, gtExpr
  | eqExpr;

eqExpr = addExpr, opEq, eqExpr
  | addExpr, opNe, eqExpr
  | addExpr;

addExpr = mulExpr, opAdd, addExpr
  | mulExpr, opSub, addExpr
  | mulExpr;

mulExpr = negExpr, opMul, mulExpr
  | negExpr, opDiv, mulExpr
  | negExpr, opMod, mulExpr
  | negExpr;

negExpr = opSub, negExpr
  | puncBang, negExpr;

indexExpr = parrenExpr, puncBracketLeft, tupleExpr, puncBracketRight
  | parrenExpr, puncParrenLeft, tupleExpr, puncParrenRight
  | parrenExpr, puncDot, indexExpr
  | parrenExpr;

parrenExpr = puncParrenLeft, expr, puncParrenRight
  | blockExpr
  | ifExpr
  | termExpr;

ifExpr = keywordIf, expr, blockExpr, keywordElse, ifExpr
  | keywordIf, expr, blockExpr, keywordElse, blockExpr
  | keywordIf, expr, blockExpr;

termExpr = literalExpr
  | identifierExpr;

identifierExpr = identifier;

literalExpr = literalBool
  | literalInt
  | literalFloat
  | literalString;